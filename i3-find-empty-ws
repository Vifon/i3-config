#!/usr/bin/env perl

use warnings;
use strict;
use Pod::Usage;

unless (@ARGV == 0) {
    pod2usage({ -verbose => 2 }) if $ARGV[0] eq "--help"
                                 or $ARGV[0] eq "-h";
}

use List::Util 'first';

use lib "$ENV{HOME}/.i3";
use i3;

# generate a N character long random string
sub rand_string {
    my $string;
    $string .= ("A".."Z","a".."z")[rand 26*2] for 1..$_[0];
    return $string;
}

my $i3 = new i3;

my @possible_workspaces = ("1".."10");
my @occupied_workspaces = $i3->workspaces;

my $empty_workspace = first                       # find the first workspace in the list
                        { my $ws = $_;            #
                          not first { $ws eq $_ } # that does not exists yet
                                    @occupied_workspaces }
                        @possible_workspaces;

# if there is no empty workspace in the possible workspace list
if (not defined $empty_workspace) {
    if (defined $ARGV[0] && $ARGV[0] eq "--random") {
        $empty_workspace = rand_string(5)
    } else {
        exit 1
    }
}

print $empty_workspace . "\n";

=head1 NAME

i3-find-empty-ws - find empty workspace

=head1 SYNOPSIS

B<i3-find-empty-ws> [I<--random>]

=head1 DESCRIPTION

Print to stdout the name of the first empty (non-existing) workspace from the
list (1..10 by default). The I<--random> switch generates a random workspace
name when there are no empty workspaces on the list.

=head1 SEE ALSO

B<i3>(1)

=head1 COPYRIGHT

Copyright (C) 2012  Wojciech Siewierski

This program is free software: you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation, either version 3 of the License, or
(at your option) any later version.

This program is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU General Public License for more details.

You should have received a copy of the GNU General Public License
along with this program.  If not, see <http://www.gnu.org/licenses/>.
